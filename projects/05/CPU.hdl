// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // instruction destination: 5, 4, 3 -> A, D, M
    // Note that there's no "M" register here, since it's just representing the destination of A
    // However, write to M only on C instruction
    And(a=instruction[15], b=instruction[3], out=writeM);

    Not(in=instruction[15], out=instructionA);
    // Determines A reg type and value
    Mux16(a=instruction, b=ALUOut, sel=instruction[15], out=ARegIn);
    Or(a=instruction[5], b=instructionA, out=loadA);
    ARegister(in=ARegIn, load=loadA, out=ARegOut, out[0..14]=addressM);

    // Combine M and A reg. to feed into ALU
    Mux16(a=ARegOut, b=inM, sel=instruction[12], out=AMRegOut);

    // D Reg: ALUOut-> D -> ALU
    And(a=instruction[15], b=instruction[4], out=writeD);
    DRegister(in=ALUOut, load=writeD, out=DOut);

    ALU(x=DOut, y=AMRegOut, zx=instruction[11], nx=instruction[10], zy=instruction[9], 
        ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUOut, out=outM, zr=zr, ng=ng);

    // Use last output of ALU and jmp directive to determine PC input
    // ALUOutput: zr:=0, ng:<0
    // instruction: 2, 1, 0 -> <0, =0, >0
    And(a=instruction[2], b=ng, out=jumpCondLess0);
    And(a=instruction[1], b=zr, out=jumpCondIs0);
    Or(a=zr, b=ng, out=nonPs);
    Not(in=nonPs,out=ps);
    
    And(a=instruction[0], b=ps, out=jumpCondGreater0);
    Or(a=jumpCondLess0, b=jumpCondGreater0, out=jumpNot0);
    Or(a=jumpNot0, b=jumpCondIs0, out=jump);
    // Only jump on c-instruction
    And(a=jump, b=instruction[15], out=jumpOnC);

    PC(in=ARegOut, load=jumpOnC, inc=true, reset=reset, out[0..14]=pc);

}